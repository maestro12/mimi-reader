buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://repo1.maven.org/maven2' }
        maven { url 'https://guardian.github.com/maven/repo-releases' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0-rc03'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.33.0'
        classpath 'com.google.firebase:perf-plugin:1.3.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.ben-manes.versions'
repositories {
    google()
    jcenter()
    maven { url 'https://maven.google.com' }
    maven { url "https://adcolony.bintray.com/AdColony" }
    maven { url 'http://guardian.github.com/maven/repo-releases' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
    maven { url 'https://jitpack.io' }
    maven { url "http://dl.bintray.com/novoda/maven" }
    maven { url "https://s3.amazonaws.com/moat-sdk-builds" }
    maven { url "https://dl.bintray.com/ironsource-mobile/android-sdk" }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30

        versionCode 230
        versionName "7.0.1"

        applicationId "com.emogoth.android.phone.mimi"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }
    buildFeatures {
        viewBinding true
    }

    lintOptions {
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
        release {
            storeFile file('release.keystore')
            keyAlias 'fake'
            keyPassword 'fake'
            storePassword 'fake'
        }
    }
    flavorDimensions "default"
    productFlavors {
        free {
            dimension "default"
        }

        paid {
            dimension "default"
            applicationIdSuffix '.donate'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            buildConfigField "boolean", "SHOW_ALL_BOARDS", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }

        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix '.beta'
            versionNameSuffix ' (DEBUG)'
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "SHOW_ALL_BOARDS", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt', 'proguard-rules-debug.txt'
            minifyEnabled true
            shrinkResources false
            debuggable true
            zipAlignEnabled true
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

allprojects {
    afterEvaluate { project ->
        def propsFile = rootProject.file('keystore.properties')
        def configName = 'release'

        if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storeFile = file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}

dependencies {
//    implementation fileTree(dir: 'src/main/libs/', include: '*.jar')

    api project(':chanlib')

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.core:core:1.3.2"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    kapt 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:1.0.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'commons-codec:commons-codec:1.12'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.21'
    implementation 'com.novoda:simple-chrome-custom-tabs:0.1.6'
    implementation 'com.google.android.exoplayer:exoplayer:2.11.7'
    implementation 'com.google.android.exoplayer:extension-okhttp:2.11.7'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.quiph.ui:recyclerviewfastscroller:0.2.3'

    def work_version = "2.4.0"

    // (Java only)
    implementation "androidx.work:work-runtime:$work_version"

    // Kotlin + coroutines
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // optional - RxJava2 support
    implementation "androidx.work:work-rxjava2:$work_version"


    implementation 'org.conscrypt:conscrypt-android:2.5.1'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version" // For Kotlin use kapt instead of annotationProcessor

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // optional - Guava support for Room, including Optional and ListenableFuture
    implementation "androidx.room:room-guava:$room_version"

    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'com.rarepebble:colorpicker:2.4.0'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    implementation 'com.squareup.tape2:tape:2.0.0-SNAPSHOT'


//    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'
    implementation 'com.github.MimiReader:AndroidPdfViewer:3.2.0-beta.2'

//    debugImplementation "com.squareup.leakcanary:leakcanary-android:$canary_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
